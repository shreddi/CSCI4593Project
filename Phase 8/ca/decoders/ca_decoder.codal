/**
 * Codasip s.r.o.
 *
 * CONFIDENTIAL
 *
 * Copyright 2022 Codasip s.r.o.
 *
 * All Rights Reserved.
 *
 * NOTICE: All information contained in this file, is and shall remain the property of
 * Codasip s.r.o. and its suppliers, if any.
 *
 * The intellectual and technical concepts contained herein are confidential and proprietary to
 * Codasip s.r.o. and are protected by trade secret and copyright law.  In addition, elements of the
 * technical concepts may be patent pending.
 *
 * This file is part of the Codasip Studio product. No part of the Studio product, including this
 * file, may be use, copied, modified, or distributed except in accordance with the terms contained
 * in Codasip license agreement under which you obtained this file.
 *
 *  \file   ca_decoder.codal
 *  \author Codasip
 *  \date   09.02.2022
 *  \brief  cycle accurate model
 */

#include "ca_defines.hcodal"
#include "opcodes.hcodal"
#include "config.hcodal"
#include "common_enums.hcodal"

// -------------------------------------------------------------------------------------------------
// Decoder
// -------------------------------------------------------------------------------------------------

set inst_decode = i_hw_halt,
        i_hw_itype_alu,
        i_hw_rtype_alu,
        i_hw_rtype_shift,
        i_hw_btype_branches,
        i_hw_jtype_jlink,
        i_hw_itype_jlreg,
        i_hw_utype_ops,
        i_hw_stype_store,
        i_hw_itype_loads;

// -------------------------------------------------------------------------------------------------
// Halt instruction
// -------------------------------------------------------------------------------------------------

// This instruction has no operands.

element i_hw_halt
{
    use opc_halt as opc;

    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable
        s_id_regwrite = false;

        // Operations
        s_id_aluop = ALU_NOP;

        // Operand MUXes
        s_id_alusrc1 = DONT_CARE;
        s_id_alusrc2 = DONT_CARE;

        // IMMEDIATE MUX select lines
        s_id_imm_gen_sel = DONT_CARE;

        // Is this a branch instruction to take a branch if branch taken
        s_id_branch_inst = false;
        s_id_jump_inst = false;

        // Load / store control signals
        s_id_mem_ops = MEM_NOP;
        s_id_memread = false;

        // HALT Command
        s_id_halt = true;

        //Branch Operation
        s_id_brnchop = BRANCH_FALSE;

        //Write ALU or PC+4 to destination register
        s_id_rfwtsel = DONT_CARE;
    };
};

// -------------------------------------------------------------------------------------------------
// Immediate Arithmetic instructions
// -------------------------------------------------------------------------------------------------

element i_hw_itype_alu
{
    use opc_itype_alu as opc;

    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable
        s_id_regwrite = true;

        // This switch statement assigns the alu operation to be performed by this instruction
        switch (opc)
        {
            case ITYPE_ADDI:
                s_id_aluop = ALU_ADD;
                break;
            case ITYPE_SLTI:
                s_id_aluop = ALU_SLTI;
                break;
            case ITYPE_SLTIU:
                s_id_aluop = ALU_SLTIU;
                break;
            case ITYPE_XORI:
                s_id_aluop = ALU_XORI;
                break;
            case ITYPE_ORI:
                s_id_aluop = ALU_ORI;
                break;
            case ITYPE_ANDI:
                s_id_aluop = ALU_ANDI;
                break;   
            default:
//                codasip_fatal(ALU_ERROR, "ALU Immediate Decoder case: %d\n", opc);
                break;
        };

        // Operand MUXes
        s_id_alusrc1 = ALU_SRC1_SEL_RF;
        s_id_alusrc2 = ALU_SRC2_SEL_IMM;

        // IMMEDIATE MUX select lines
        s_id_imm_gen_sel = ITYPE_IMM_SEL;

        // Is this a branch instruction to take a branch if branch taken
        s_id_branch_inst = false;
        s_id_jump_inst = false;

        // Load / store control signals
        s_id_mem_ops = MEM_NOP;
        s_id_memread = false;

        // HALT Command
        s_id_halt = false;

        //Branch Operation
        s_id_brnchop = BRANCH_FALSE;

        //Write ALU or PC+4 to destination register
        s_id_rfwtsel = WB_ALU;
    };
};

// -------------------------------------------------------------------------------------------------
// Arithmetic instructions
// -------------------------------------------------------------------------------------------------

element i_hw_rtype_alu
{
   use opc_rtype_alu as opc;

   assembly { opc };
   binary { opc };

   semantics
   {
       // Register file write enable
       s_id_regwrite = true;

       // This switch statement assigns the alu operation to be performed by this instruction
       switch (opc)
       {
           case RTYPE_ADD:
                s_id_aluop = ALU_ADD;
                break;
            case RTYPE_SUB:
                s_id_aluop = ALU_SUB;
                break;
            case RTYPE_SLL:
                s_id_aluop = ALU_SLL;
                break;
            case RTYPE_SLT:
                s_id_aluop = ALU_SLTI;
                break;
            case RTYPE_SLTU:
                s_id_aluop = ALU_SLTIU;
                break;
            case RTYPE_XOR:
                s_id_aluop = ALU_XORI;
                break;
            case RTYPE_SRL:
                s_id_aluop = ALU_SRL;
                break;
            case RTYPE_OR:
                s_id_aluop = ALU_ORI;
                break;
            case RTYPE_AND:
                s_id_aluop = ALU_ANDI;
                break;
            case RTYPE_SRA:
                s_id_aluop = ALU_SRA;
                break;
            default:
//                codasip_fatal(ALU_ERROR, "ALU Immediate Decoder case: %d\n", opc);
                break;
       };

       // Operand MUXes
       s_id_alusrc1 = ALU_SRC1_SEL_RF;
       s_id_alusrc2 = ALU_SRC2_SEL_RF;

       // IMMEDIATE MUX select lines
       s_id_imm_gen_sel = RTYPE_IMM_SEL;

       // Is this a branch instruction to take a branch if branch taken
       s_id_branch_inst = false;
       s_id_jump_inst = false;

       // Load / store control signals
       s_id_mem_ops = MEM_NOP;
       s_id_memread = false;

       // HALT Command
       s_id_halt = false;

       //Branch Operation
       s_id_brnchop = BRANCH_FALSE;

       //Write ALU or PC+4 to destination register
       s_id_rfwtsel = WB_ALU;
   };
};

// -------------------------------------------------------------------------------------------------
// Rtype Immediate shift instructions
// -------------------------------------------------------------------------------------------------

element i_hw_rtype_shift
{
   use opc_rtype_shift as opc;

   assembly { opc };
   binary { opc };

   semantics
   {
       // Register file write enable
       s_id_regwrite = true;

       // This switch statement assigns the alu operation to be performed by this instruction
       switch (opc)
       {
           case RTYPE_SLLI:
                s_id_aluop = ALU_SLL;
                break;
            case RTYPE_SRLI:
                s_id_aluop = ALU_SRL;
                break;
            case RTYPE_SRAI:
                s_id_aluop = ALU_SRA;
                break;
            default:
//                codasip_fatal(ALU_ERROR, "ALU Immediate Decoder case: %d\n", opc);
                break;
       };

       // Operand MUXes
       s_id_alusrc1 = ALU_SRC1_SEL_RF;
       s_id_alusrc2 = ALU_SRC2_SEL_IMM;

       // IMMEDIATE MUX select lines
       s_id_imm_gen_sel = ITYPE_IMM_SEL;

       // Is this a branch instruction to take a branch if branch taken
       s_id_branch_inst = false;
       s_id_jump_inst = false;

       // Load / store control signals
       s_id_mem_ops = MEM_NOP;
       s_id_memread = false;

       // HALT Command
       s_id_halt = false;

       //Branch Operation
       s_id_brnchop = BRANCH_FALSE;

       //Write ALU or PC+4 to destination register
       s_id_rfwtsel = WB_ALU;
   };
};


// -------------------------------------------------------------------------------------------------
// Branch instructions
// -------------------------------------------------------------------------------------------------

element i_hw_btype_branches //ALUOP?
{
   use opc_btype_branches as opc;

   assembly { opc };
   binary { opc };

   semantics
   {
       // Register file write enable
       s_id_regwrite = false;

       // This switch statement assigns the alu operation to be performed by this instruction and branch operation
       switch (opc)
       {
            case BTYPE_BEQ:
                s_id_aluop = ALU_SUB;
                s_id_brnchop = BRANCH_COND_TRUE; //branch taken if the result of the ALU is zero.
                break;
            case BTYPE_BGE:
                s_id_aluop = ALU_SLTI;
                s_id_brnchop = BRANCH_COND_TRUE; //branch taken if the result of the ALU is zero.
                break;
            case BTYPE_BGEU: //branch taken if the result of the ALU is zero.
                s_id_aluop = ALU_SLTIU;
                s_id_brnchop = BRANCH_COND_TRUE;
                break;
            case BTYPE_BLT: //branch taken if the result of the ALU is NOT zero.
                s_id_aluop = ALU_SLTI;
                s_id_brnchop = BRANCH_COND_FALSE;
                break;
            case BTYPE_BLTU: //branch taken if the result of the ALU is NOT zero.
                s_id_aluop = ALU_SLTIU;
                s_id_brnchop = BRANCH_COND_FALSE;
                break;
            case BTYPE_BNE: //branch taken if the result of the ALU is NOT zero.
                s_id_aluop = ALU_SUB;
                s_id_brnchop = BRANCH_COND_FALSE;
                break;
            default:
                s_id_aluop = 0;
                s_id_brnchop = 0;
                break;
       };

       // Operand MUXes
       s_id_alusrc1 = ALU_SRC1_SEL_RF;
       s_id_alusrc2 = ALU_SRC2_SEL_RF;

       // IMMEDIATE MUX select lines
       s_id_imm_gen_sel = BTYPE_IMM_SEL;

       // Is this a branch instruction to take a branch if branch taken
       s_id_branch_inst = true;
       s_id_jump_inst = false;

       // Load / store control signals
       s_id_mem_ops = MEM_NOP;
       s_id_memread = false;

       // HALT Command
       s_id_halt = false;

       //Write ALU or PC+4 to destination register
       s_id_rfwtsel = DONT_CARE;
   };
};

// -------------------------------------------------------------------------------------------------
// LIU and AUIPC and LUI instructions
// -------------------------------------------------------------------------------------------------

element i_hw_utype_ops
{
   use opc_utype_ops as opc;

   assembly { opc };
   binary { opc };

   semantics
   {
       // Register file write enable
       s_id_regwrite = true;

    
       // This switch statement assigns the alu operation to be performed by this instruction
       switch (opc)
       {
            case UTYPE_LUI:
                s_id_alusrc1 = ALU_SRC1_SEL_ZERO;
                break;
            case UTYPE_AUIPC:
                s_id_alusrc1 = ALU_SRC1_SEL_PC;
                break;
            default:
                break;
       };   

        s_id_aluop = ALU_ADD;

       // Operand MUXes
       s_id_alusrc2 = ALU_SRC2_SEL_IMM;

       // IMMEDIATE MUX select lines
       s_id_imm_gen_sel = UTYPE_IMM_SEL;

       // Is this a branch instruction to take a branch if branch taken
       s_id_branch_inst = false;
       s_id_jump_inst = false;

       // Load / store control signals
       s_id_mem_ops = MEM_NOP;
       s_id_memread = false;

       // HALT Command
       s_id_halt = false;
   };
};

// -------------------------------------------------------------------------------------------------
// JAL instruction
// -------------------------------------------------------------------------------------------------

element i_hw_jtype_jlink
{
   use opc_jtype_jlink as opc;

   assembly { opc };
   binary { opc };

   semantics
   {
        // Register file write enable
        s_id_regwrite = true;

        //aluop
        s_id_aluop = DONT_CARE;

        // Operand MUXes
        s_id_alusrc1 = ALU_SRC1_SEL_RF;
        s_id_alusrc2 = ALU_SRC2_SEL_IMM;

        // IMMEDIATE MUX select lines
        s_id_imm_gen_sel = JTYPE_IMM_SEL;

        // Is this a branch instruction to take a branch if branch taken
        s_id_branch_inst = true;
        s_id_jump_inst = false;

        // Load / store control signals
        s_id_mem_ops = MEM_NOP;
        s_id_memread = false;

        // HALT Command
        s_id_halt = false;

        //Branch Operation
        s_id_brnchop = BRANCH_TRUE;

        //Write ALU or PC+4 to destination register
        s_id_rfwtsel = WB_PC;
    };
};

// -------------------------------------------------------------------------------------------------
// JALR instruction
// -------------------------------------------------------------------------------------------------
element i_hw_itype_jlreg
{
   use opc_itype_jlreg as opc;

   assembly { opc };
   binary { opc };

   semantics
   {
        // Register file write enable
        s_id_regwrite = true;

        //Aluop
        s_id_aluop = ALU_ADD; 

        // This switch statement assigns the alu operation to be performed by this instruction
        // switch (opc)
        // {

        // };

        // Operand MUXes
        s_id_alusrc1 = ALU_SRC1_SEL_RF;
        s_id_alusrc2 = ALU_SRC2_SEL_IMM;

        // IMMEDIATE MUX select lines
        s_id_imm_gen_sel = ITYPE_IMM_SEL;

        // Is this a branch instruction to take a branch if branch taken
        s_id_branch_inst = false;
        s_id_jump_inst = true;

        // Load / store control signals
        s_id_mem_ops = MEM_NOP;
        s_id_memread = false;

        // HALT Command
        s_id_halt = false;

        //Branch Operation
        s_id_brnchop = BRANCH_TRUE;

        //Write ALU or PC+4 to destination register
        s_id_rfwtsel = WB_PC;
   };
};

// -------------------------------------------------------------------------------------------------
// load instructions
// -------------------------------------------------------------------------------------------------

element i_hw_itype_loads
{
   use opc_itype_loads as opc;

   assembly { opc };
   binary { opc };

   semantics
   {
       // Register file write enable
       s_id_regwrite = true;

       // Assign the aluop opcode to perform the address calculation
       s_id_aluop = ALU_ADD;

       // This switch statement assigns the alu operation to be performed by this instruction
       switch (opc)
       {
            case ITYPE_LB:
                s_id_mem_ops = MEM_LB;
                break;
            case ITYPE_LH:
                s_id_mem_ops = MEM_LH;
                break;
            case ITYPE_LW:
                s_id_mem_ops = MEM_LW;
                break;
            case ITYPE_LBU:
                s_id_mem_ops = MEM_LBU;
                break;
            case ITYPE_LHU:
                s_id_mem_ops = MEM_LHU;
                break;
            default:
                s_id_mem_ops = MEM_NOP;
                break;
       };

       // Operand MUXes
       s_id_alusrc1 = ALU_SRC1_SEL_RF;
       s_id_alusrc2 = ALU_SRC2_SEL_IMM;

       // IMMEDIATE MUX select lines
       s_id_imm_gen_sel = ITYPE_IMM_SEL;

       // Is this a branch instruction to take a branch if branch taken
       s_id_branch_inst = false;
       s_id_jump_inst = false;

       // Load / store control signals
       s_id_memread = true;

       // HALT Command
       s_id_halt = false;
   };
};


// -------------------------------------------------------------------------------------------------
// store instructions
// -------------------------------------------------------------------------------------------------

element i_hw_stype_store
{
   use opc_stype_store as opc;

   assembly { opc };
   binary { opc };

   semantics
   {
       // Register file write enable
       s_id_regwrite = false;

       // Assign the aluop opcode to perform the address calculation
       s_id_aluop = ALU_ADD;

       // This switch statement assigns the alu operation to be performed by this instruction
       switch (opc)
       {
           case STYPE_SB:
                s_id_mem_ops = MEM_SB;
                break;
            case STYPE_SH:
                s_id_mem_ops = MEM_SH;
                break;
            case STYPE_SW:
                s_id_mem_ops = MEM_SW;
                break;
            default:
                s_id_mem_ops = MEM_NOP;
                break;
       };

       // Operand MUXes
       s_id_alusrc1 = ALU_SRC1_SEL_RF;
       s_id_alusrc2 = ALU_SRC2_SEL_IMM;

       // IMMEDIATE MUX select lines
       s_id_imm_gen_sel = STYPE_IMM_SEL;

       // Is this a branch instruction to take a branch if branch taken
       s_id_branch_inst = false;
       s_id_jump_inst = false;

       // Load / store control signals
       s_id_memread = false;

       // HALT Command
       s_id_halt = false;
   };
};
